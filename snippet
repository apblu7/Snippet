@echo off
setlocal

set "PROJECT_NAME=ets"
set "DEPLOY_PATH=/home/ubuntu"
set "KEY_PATH=C:\Keys\server_key"
set "SERVER=ubuntu@10.110.30.50"

echo [Step 3] Creating backup directory on server...
ssh -i "%KEY_PATH%" %SERVER% "timestamp=$(date +%%Y%%m%%d-%%H%%M%%S) && mkdir -p /home/deploy/${timestamp}-%PROJECT_NAME% && echo /home/deploy/${timestamp}-%PROJECT_NAME%" > tmp_backup_path.txt

if %errorlevel% neq 0 (
    echo [Error] Failed to create backup directory.
    exit /b 1
)

set /p BACKUP_PATH=<tmp_backup_path.txt
echo [Debug] Backup path is: %BACKUP_PATH%

echo [Step 4] Copying deployed file to backup directory...
ssh -i "%KEY_PATH%" %SERVER% "cp %DEPLOY_PATH%/%PROJECT_NAME%.war %BACKUP_PATH%/"

if %errorlevel% neq 0 (
    echo [Error] Failed to copy file to backup directory.
    exit /b 1
)

echo [Success] File backed up to %BACKUP_PATH%
del tmp_backup_path.txt
endlocal